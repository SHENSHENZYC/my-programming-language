statements  : NEWLINE* statement (NEWLINE+ statement)* NEWLINE*

statement   : KEYWORD:return expr?
            : KEYWORD:continue
            : KEYWORD:break
            : expr

expr        : KEYWORD:var IDENTIFIER EQ expr
            : comp-expr ((KEYWORD:and|KEYWORD:or) comp-expr)*

comp_expr   : KEYWORD:not comp-expr
            : arith-expr ((EE|LT|LTE|GT|GTE) arith-expr)*
       
arith-expr  : term ((PLUS|MINUS) term)*

term        : factor ((MUL|DIV) factor)*

factor      : (PLUS|MINUS) factor
            : power

power       : call (POW factor)*

call        : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom        : INT|FLOAT|IDENTIFIER|STRING
            : LPAREN expr RPAREN
            : if-expr
            : for-expr
            : while-expr
            : func-def
            : list-expr

list-expr   : LSQUARE (expr (COMMA expr)*)? RSQUARE

if-expr     : KEYWORD:if expr KEYWORD:then
              (statement elif-expr|else-expr?)
              | (NEWLINE statements KEYWORD:end|elif-expr|else-expr)

elif-expr   : KEYWORD:elif expr KEYWORD:then
              (statement elif-expr|else-expr?)
              | (NEWLINE statements KEYWORD:end|elif-expr|else-expr)

else-expr   : KEYWORD:else statement|(NEWLINE statements KEYWORD:end)

for-expr    : KEYWORD:for IDENTIFIER EQ expr KEYWORD:to expr
              (KEYWORD:step expr)? KEYWORD:do 
              statement|(NEWLINE statements KEYWORD:end)

while-expr  : KEYWORD:while expr KEYWORD:do 
              statement|(NEWLINE statements KEYWORD:end)

func-def    : KEYWORD:func IDENTIFIER?
              LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
              (ARROW expr)|(NEWLINE statements KEYWORD:end)
